mapscripts GhostShip_B1F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		clearflag(FLAG_GHOST_SHIP_HIDE_DUSCLOPS)
		showobjectat(1, MAP_GHOST_SHIP_B1F)
		getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_1) == 17 && var(VAR_GHOST_SHIP_STATE) == 0) {
			setobjectxyperm(1, 10, 22)
		}
		elif ((var(VAR_TEMP_0) == 25 || var(VAR_TEMP_0) == 41) && var(VAR_GHOST_SHIP_STATE) == 0) {
			setobjectxyperm(1, 34, 20)
		}
		else {
			setobjectxyperm(1, 0, 0)
		}
		setflag(FLAG_GHOST_SHIP_HIDE_EVENT_DUSCLOPS)
		removeobject(3)
	}
	MAP_SCRIPT_ON_LOAD {
		//lock doors if not unlocked yet:
		if (!flag(FLAG_UNLOCKED_CAPTAINS_QUARTERS)) {
			setmetatile(32, 29, METATILE_Pirate_Ship_Door_Locked, TRUE)
		}
		if (!flag(FLAG_UNLOCKED_SECRET_CABIN)) {
			setmetatile(38, 30, METATILE_Pirate_Ship_Door_Locked, TRUE)
		}
		//lock door if dusclops battle is still active:
		if (var(VAR_GHOST_SHIP_STATE) == 1)
		{
			setmetatile(17, 9, METATILE_Pirate_Ship_SouthExitLeftNoArrow, FALSE)
			setmetatile(18, 9, METATILE_Pirate_Ship_SouthExitRightNoArrow, FALSE)
		}
	}
}

script GhostShip_B1F_EventScript_CaptainsQuartersDoor {
	lock
	if (flag(FLAG_UNLOCKED_CAPTAINS_QUARTERS)) {
		msgbox(format("The door is unlocked."), MSGBOX_DEFAULT)
		release
		end
	}
	msgbox(format("The door is locked."), MSGBOX_DEFAULT)
	checkitem(ITEM_ELEGANT_KEY)
	if (var(VAR_RESULT)) {
		msgbox(format("Use the ELEGANT KEY?"), MSGBOX_YESNO)
		if (!var(VAR_RESULT)) {
			release
			end
		}
		delay(10)
		playse(SE_UNLOCK)
		waitse
		setmetatile(32, 29, METATILE_Pirate_Ship_Door_Unlocked, TRUE)
		special(DrawWholeMapView)
		setflag(FLAG_UNLOCKED_CAPTAINS_QUARTERS)
		msgbox(format("The door is unlocked."), MSGBOX_DEFAULT)
	}
	release
	end
}

script GhostShip_B1F_EventScript_SecretCabinDoor {
	lock
	if (flag(FLAG_UNLOCKED_SECRET_CABIN)) {
		msgbox(format("The door is unlocked."), MSGBOX_DEFAULT)
		release
		end
	}
	msgbox(format("The door is locked."), MSGBOX_DEFAULT)
	checkitem(ITEM_OLD_KEY)
	if (var(VAR_RESULT)) {
		msgbox(format("Use the OLD KEY?"), MSGBOX_YESNO)
		if (!var(VAR_RESULT)) {
			release
			end
		}
		delay(10)
		playse(SE_UNLOCK)
		waitse
		setmetatile(38, 30, METATILE_Pirate_Ship_Door_Unlocked, TRUE)
		special(DrawWholeMapView)
		setflag(FLAG_UNLOCKED_SECRET_CABIN)
		msgbox(format("The door is unlocked."), MSGBOX_DEFAULT)
	}
	release
	end
}

script GhostShip_B1F_EventScript_ItemDiveBall {
	finditem(ITEM_DIVE_BALL)
	end
}

script GhostShip_B1F_EventScript_ItemTM30 {
	finditem(ITEM_TM30)
	end
}

script GhostShip_B1F_EventScript_ItemElegantKey {
	finditem(ITEM_ELEGANT_KEY)
	end
}

script GhostShip_B1F_EventScript_GalleySign {
	msgbox(format("GALLEY"), MSGBOX_SIGN)
	end
}

script GhostShip_B1F_EventScript_StoreRoomSign {
	msgbox(format("STORE ROOM"), MSGBOX_SIGN)
	end
}

script GhostShip_B1F_EventScript_CaptainsQuartersSign {
	msgbox(format("CAPTAIN'S QUARTERS"), MSGBOX_SIGN)
	end
}

script GhostShip_B1F_HideDusclopsTrigger {
	setflag(FLAG_GHOST_SHIP_HIDE_DUSCLOPS)
	removeobject(1)
	end
}

script GhostShip_EventScript_ItemHM08 {
	finditem(ITEM_HM08)
	setflag(FLAG_RECEIVED_HM08)
	setvar(VAR_GHOST_SHIP_STATE, 1)
	setmetatile(17, 9, METATILE_Pirate_Ship_SouthExitLeftNoArrow, FALSE)
	setmetatile(18, 9, METATILE_Pirate_Ship_SouthExitRightNoArrow, FALSE)
	end
}

script GhostShip_EventScript_DusclopsEncounter {
	lock
	playse(SE_CLICK)
	delay(40)
	msgbox(format("The door is locked..."))
	playmoncry(SPECIES_DUSCLOPS, 2)
	delay(40)
	waitmoncry
	fadescreen(1)
	msgbox(format(".......mine...."))
	closemessage
	clearflag(FLAG_GHOST_SHIP_HIDE_EVENT_DUSCLOPS)
	addobject(3)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	fadescreen(0)
	delay(40)
	applymovement(3, DusclopsAppears)
	waitmovement(0)
	playmoncry(SPECIES_DUSCLOPS, 0)
	msgbox(format("....off........\nmy.....ship...."))
	//setwildbattle(SPECIES_DUSCLOPS, 50, ITEM_NONE)
	special(SetDusclopsBattle)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	dowildbattle
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_WON || var(VAR_RESULT) == B_OUTCOME_RAN || var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED) {
		fadescreen(1)
		setflag(FLAG_GHOST_SHIP_HIDE_EVENT_DUSCLOPS)
		removeobject(3)
		fadescreen(0)
		delay(40)
		playse(SE_UNLOCK)
		delay(40)
		msgbox(format("The door was unlocked."))
		setmetatile(17, 9, METATILE_Pirate_Ship_SouthExitLeft, FALSE)
		setmetatile(18, 9, METATILE_Pirate_Ship_SouthExitRight, FALSE)
		setvar(VAR_GHOST_SHIP_STATE, 2)
	}
	setflag(FLAG_GHOST_SHIP_HIDE_EVENT_DUSCLOPS)
	removeobject(3)
	release
	end
}

movement DusclopsAppears {
	set_visible
	delay_4
	set_invisible
	delay_4
	set_visible
	delay_4
	set_invisible
	delay_4
	set_visible
	delay_8
	set_invisible
	delay_8
	set_visible
	delay_8
	set_invisible
	delay_8
	set_visible
	delay_16
	set_invisible
	delay_16
	set_visible
}