mapscripts GatewayIsland_PlayersHouse_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setrespawn(HEAL_LOCATION_GATEWAY_ISLAND)
	}
}

script GatewayIsland_PlayersHouse_EventScript_Grandma {
	lock
	faceplayer
	if (!flag(FLAG_RECEIVED_POKENAV)) { //haven't received pokenav yet
		msgbox(format("GRANDMA: Ah, {PLAYER}!\nFinally got yourself out of bed, eh?\pI left you a very special present on the table.\lDid you pick it up yet?\pOh, that's right!\nI have something else important\lfor a new TRAINER like yourself."), MSGBOX_DEFAULT)
		setflag(FLAG_SYS_POKENAV_GET)
		setflag(FLAG_RECEIVED_POKENAV)
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox("{PLAYER} received the POKéNAV!", MSGBOX_DEFAULT)
		waitfanfare
		msgbox(format("This will help you navigate when you're out exploring.\lDon't go and get yourself lost, now!"), MSGBOX_DEFAULT)
		msgbox(GatewayIsland_PlayersHouse_Text_GrandmaComeBackLater, MSGBOX_DEFAULT)
		release
		end
	}
	if (!flag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)) { //haven't received pokedex yet
		msgbox(GatewayIsland_PlayersHouse_Text_GrandmaComeBackLater, MSGBOX_DEFAULT)
		release
		end
	}
	if (!flag(FLAG_RECEIVED_REGIUS_CREST)) { //haven't received regius crest yet
		msgbox(format("So she gave you a POKéDEX, eh?\pNow you're really prepared to start your journey!\pBut before you go, I have one more gift for you.\pGo on, take it."), MSGBOX_DEFAULT)
		giveitem(ITEM_REGIUS_CREST, 1)
		setflag(FLAG_RECEIVED_REGIUS_CREST)
		msgbox(format("This odd charm has been passed down in my family for generations.\pI hope it will keep you safe and bring you good fortune on your adventure.\pDon't forget, if you grow weary from your travels, you can always come back here and rest in your bed.\pThere's no shame in taking a break when you need it!\pNow, what are you waiting for?\nGet out there and have fun!"), MSGBOX_DEFAULT)
		release
		end
	}
	msgbox(format("All this talk of adventure has me thinking about my youth.\pYou might inspire me to go out and have an adventure of my own!\lWouldn't that be something?"), MSGBOX_DEFAULT)
	release
	end
}

script GatewayIsland_PlayersHouse_ItemBall {
	lock
	msgbox(format("It's a gift from GRANDMA.\nTake it?"), MSGBOX_YESNO)
	if (!var(VAR_RESULT)) {
		release
		end
	}
    removeobject(1)
	givemon(SPECIES_PLAKOMASK, 15, ITEM_NONE)
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox("{PLAYER} received PLAKOMASK!", MSGBOX_DEFAULT)
	waitfanfare
    msgbox(format("Would you like to give a\nnickname to your PLAKOMASK?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)){
    	call(Common_EventScript_NameReceivedPartyMon)
    }
	setflag(FLAG_SYS_POKEMON_GET)
	release
	end
}

script GatewayIsland_PlayersHouse_EventScript_PlayersBed {
	lock
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	msgbox(format("Would you like to take\na rest?"), MSGBOX_YESNO)
	closemessage
	if (var(VAR_RESULT)) {
		fadescreen(1)
		playfanfare(MUS_HEAL)
		special(HealPlayerParty)
		waitfanfare
		fadescreen(0)
		msgbox(format("You and your POKéMON\nare feeling refreshed!"), MSGBOX_AUTOCLOSE)
	}
	release
	end
}

script GatewayIsland_PlayersHouse_EventScript_GrandmasBed {
	msgbox(format("GRANDMA's bed.\nBetter not mess with it!"), MSGBOX_SIGN)
}

text GatewayIsland_PlayersHouse_Text_GrandmaComeBackLater {
	format("There's a nice lady nextdoor who wants to see you, too.\lWhy don't you pay her a visit?\pOh, and I left some more supplies for you in the PC.\pDon't forget to check the PC before you leave!\pCome speak with me again once you've done all that.")
}