mapscripts GatewayIsland_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_GATEWAY_ISLAND)
		if (!flag(FLAG_SYS_POKEMON_GET)) { //move kecleon boy if the player has no pokemon
			setobjectxyperm(2, 39, 23)
			setobjectxyperm(3, 39, 24)
			setobjectmovementtype(2, MOVEMENT_TYPE_FACE_LEFT)
			setobjectmovementtype(3, MOVEMENT_TYPE_FACE_LEFT)
		}
	}
	MAP_SCRIPT_ON_LOAD {
		if (!flag(FLAG_REGIROCK_SEAL_OPEN)) {
			setmetatile(25, 12, METATILE_Fortree_RegiSeal_Off, TRUE)
			setmetatile(29, 12, METATILE_Fortree_RegiSeal_Off, TRUE)
		}
		if (!flag(FLAG_REGICE_SEAL_OPEN)) {
			setmetatile(25, 13, METATILE_Fortree_RegiSeal_Off, TRUE)
			setmetatile(29, 13, METATILE_Fortree_RegiSeal_Off, TRUE)
		}
		if (!flag(FLAG_REGISTEEL_SEAL_OPEN)) {
			setmetatile(25, 14, METATILE_Fortree_RegiSeal_Off_Bottom, TRUE)
			setmetatile(29, 14, METATILE_Fortree_RegiSeal_Off_Bottom, TRUE)
		}
		if (!flag(FLAG_GATEWAY_DOOR_OPEN)) {
			setmetatile(27, 13, METATILE_Fortree_Gateway_Entrance_Cover_Top, TRUE)
			setmetatile(27, 14, METATILE_Fortree_Gateway_Entrance_Cover_Bottom, TRUE)
		}
	}
}

script GatewayIsland_EventScript_StoneGate {
	lock
	setvar(VAR_TEMP_1, SPECIES_REGIROCK)
	specialvar(VAR_RESULT, GetCaughtMon)
	if (var(VAR_RESULT) && !flag(FLAG_REGIROCK_SEAL_OPEN)) { //if regirock is caught and the seal isn't open, update gate state
		goto(GatewayIsland_EventScript_UpdateGateState)
	}
	setvar(VAR_TEMP_1, SPECIES_REGICE)
	specialvar(VAR_RESULT, GetCaughtMon)
	if (var(VAR_RESULT) && !flag(FLAG_REGICE_SEAL_OPEN)) { //if regice is caught and the seal isn't open, update gate state
		goto(GatewayIsland_EventScript_UpdateGateState)
	}
	setvar(VAR_TEMP_1, SPECIES_REGISTEEL)
	specialvar(VAR_RESULT, GetCaughtMon)
	if (var(VAR_RESULT) && !flag(FLAG_REGISTEEL_SEAL_OPEN)) { //if registeel is caught and the seal isn't open, update gate state
		goto(GatewayIsland_EventScript_UpdateGateState)
	}
	if (flag(FLAG_GATEWAY_DOOR_OPEN)) {
		msgbox("The way is open.", MSGBOX_AUTOCLOSE)
	}
	else {
		braillemsgbox(GatewayIsland_Braille_StoneGate1)
		delay(5)
		braillemsgbox(GatewayIsland_Braille_StoneGate2)
	}
	release
	end
}

script GatewayIsland_EventScript_UpdateGateState {
	fadeoutbgm(1)
	setvar(VAR_0x8004, 0)  //vertical pan
	setvar(VAR_0x8005, 1)  //horizontal pan
	setvar(VAR_0x8006, 8)  //num shakes
	setvar(VAR_0x8007, 5)  //shake delay
	special(ShakeCamera)
	waitstate
	delay(30)
	setvar(VAR_TEMP_1, SPECIES_REGIROCK)
	specialvar(VAR_RESULT, GetCaughtMon)
	if (var(VAR_RESULT) && !flag(FLAG_REGIROCK_SEAL_OPEN)) { //if regirock is caught and the seal isn't open, open the seal
		setmetatile(25, 12, METATILE_Fortree_RegiSeal_Red, TRUE)
		setmetatile(29, 12, METATILE_Fortree_RegiSeal_Red, TRUE)
		special(DrawWholeMapView)
		playse(SE_UNLOCK)
		setflag(FLAG_REGIROCK_SEAL_OPEN)
		delay(30)
	}
	setvar(VAR_TEMP_1, SPECIES_REGICE)
	specialvar(VAR_RESULT, GetCaughtMon)
	if (var(VAR_RESULT) && !flag(FLAG_REGICE_SEAL_OPEN)) { //if regice is caught and the seal isn't open, open the seal
		setmetatile(25, 13, METATILE_Fortree_RegiSeal_Blue, TRUE)
		setmetatile(29, 13, METATILE_Fortree_RegiSeal_Blue, TRUE)
		special(DrawWholeMapView)
		playse(SE_UNLOCK)
		setflag(FLAG_REGICE_SEAL_OPEN)
		delay(30)
	}
	setvar(VAR_TEMP_1, SPECIES_REGISTEEL)
	specialvar(VAR_RESULT, GetCaughtMon)
	if (var(VAR_RESULT) && !flag(FLAG_REGISTEEL_SEAL_OPEN)) { //if registeel is caught and the seal isn't open, open the seal
		setmetatile(25, 14, METATILE_Fortree_RegiSeal_Gray, TRUE)
		setmetatile(29, 14, METATILE_Fortree_RegiSeal_Gray, TRUE)
		special(DrawWholeMapView)
		playse(SE_UNLOCK)
		setflag(FLAG_REGISTEEL_SEAL_OPEN)
		delay(30)
	}
	delay(30)
	if (flag(FLAG_REGIROCK_SEAL_OPEN) && flag(FLAG_REGICE_SEAL_OPEN) && flag(FLAG_REGISTEEL_SEAL_OPEN)) { //if all three seals are open, open the door
		setmetatile(27, 13, METATILE_Fortree_Gateway_Entrance_Top, TRUE)
		setmetatile(27, 14, METATILE_Fortree_Gateway_Entrance_Bottom, FALSE)
		special(DrawWholeMapView)
		playse(SE_UNLOCK)
		setflag(FLAG_GATEWAY_DOOR_OPEN)
		delay(30)
	}
	fadeinbgm(0)
	release
	end
}

script GatewayIsland_EventScript_KecleonBoy {
	lock
	faceplayer
	if (flag(FLAG_SYS_POKEMON_GET)) {
		msgbox(format("These islands are chock full of\nwild POKéMON.\pIt's wonderful, but it can be\ndangerous if you don't have a\lPOKéMON to protect you."), MSGBOX_AUTOCLOSE)
	}
	else {
		msgbox(format("There are wild POKéMON out this way.\nI can't let you go out there unless\lyou have a POKéMON of your own."), MSGBOX_AUTOCLOSE)
	}
	release
	end
}

script GatewayIsland_EventScript_LittleGirl {
	lock
	faceplayer
	msgbox(format("Hi!\nDo you have any cute POKéMON?"), MSGBOX_AUTOCLOSE)
	release
	end
}

script GatewayIsland_EventScript_Kecleon {
	lock
	faceplayer
	playmoncry(SPECIES_KECLEON, 0)
	msgbox("Kiyahn!", MSGBOX_AUTOCLOSE)
	waitmoncry
	release
	end
}

script GatewayIsland_EventScript_Lass {
	lock
	faceplayer
	checkpartytmhm(ITEM_HM07)
	if (var(VAR_RESULT) == PARTY_SIZE) {
		msgbox(format("Hmm... None of your POKéMON can learn WATERFALL."), MSGBOX_AUTOCLOSE)
	}
	else {
		bufferpartymonnick(0, VAR_RESULT)
		msgbox(format("Oh! Your {STR_VAR_1}\ncan learn WATERFALL!"), MSGBOX_AUTOCLOSE)
	}
	release
	end
}

script GatewayIsland_EventScript_Woman {
	msgbox(format("Sigh…\pLife on the island can be\na bit uneventful.\lBut I wouldn't trade it for anything."), MSGBOX_NPC)
	end
}

script GatewayIsland_EventScript_GateMan {
	if (flag(FLAG_GATEWAY_DOOR_OPEN)) {
		msgbox(format("Huh? The gate is open!\nDid you do that…?"), MSGBOX_NPC)
	}
	else {
		msgbox(format("This stone gate has been here as long\nas anyone can remember.\pApparently it was left by some\nancient civilization.\pNo one has been able to see what's\nbehind it, either.\lIt's quite an enigma."), MSGBOX_NPC)
	}
	end
}

script GatewayIsland_EventScript_SouthBeachBoy {
	msgbox(format("The trainers down this way are\ntoo tough!\pIf you're a newbie trainer,\nyou might want to rethink\lgoing down south."), MSGBOX_NPC)
	end
}

script GatewayIsland_EventScript_FlyingTaxi {
	lock
	faceplayer
	msgbox(format("Want a lift?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == 1)
	{
		setvar(VAR_0x800A, LAST_TALKED_TO_FLYING_TAXI)
		special(FieldLoadFlyMap)
	}
	release
	end
}

script GatewayIsland_EventScript_ItemPokeBall {
	finditem(ITEM_POKE_BALL)
	end
}

raw `
GatewayIsland_Braille_StoneGate1:
	brailleformat 3, 0, 25, 19, 6, 3
	.braille "AWAKEN THE\n"
	.braille "CHILDREN OF\n"
	.braille "ROCK, ICE,\n"
	.braille "AND STEEL.$"
	
GatewayIsland_Braille_StoneGate2:
	brailleformat 3, 0, 25, 19, 6, 3
	.braille "THE RESTING\n"
	.braille "PLACE OF\n"
	.braille "THE COLOSSUS\n"
	.braille "WILL OPEN.$"
`