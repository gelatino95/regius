mapscripts IgneousCaldera_1F_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        if (!flag(FLAG_IGNEOUS_CALDERA_CHAMBER_OPEN)) { //if chamber hasn't been opened yet, close the door
            setmetatile(18, 35, METATILE_Cave_EntranceCover, TRUE)
            setmetatile(19, 35, METATILE_Cave_EntranceCover, TRUE)
            setmetatile(20, 35, METATILE_Cave_EntranceCover, TRUE)
            setmetatile(18, 36, METATILE_Cave_SealedChamberBraille_Mid, TRUE)
            setmetatile(19, 36, METATILE_Cave_SealedChamberBraille_Mid, TRUE)
            setmetatile(20, 36, METATILE_Cave_SealedChamberBraille_Mid, TRUE)
        }
    }
}

script IgneousCaldera_1F_EventScript_ItemBurnHeal {
    finditem(ITEM_BURN_HEAL)
    end
}

script IgneousCaldera_1F_EventScript_ItemAncientSlab {
    finditem(ITEM_ANCIENT_SLAB)
    end
}

script IgneousCaldera_1F_EventScript_Ortega {
    trainerbattle(TRAINER_BATTLE_DETECT, TRAINER_ORTEGA, 0, format("Hey hey!\nDon't let the heat get to ya!"), format("Hot, hot!"))
	msgbox(format("Aw yeah! This heat is doing something for me!\pOr is it the toxic fumes…?"), MSGBOX_NPC)
    end
}

script IgneousCaldera_1F_EventScript_Zhao {
    trainerbattle(TRAINER_BATTLE_DETECT, TRAINER_ZHAO, 0, format("Excuse me! Let me see your POKéMON!"), format("Not what I hoped…"))
	msgbox(format("This place is swarming with unique POKéMON! I'm gonna find them all!"), MSGBOX_NPC)
    end
}

script IgneousCaldera_1F_EventScript_Raul {
    trainerbattle(TRAINER_BATTLE_DETECT, TRAINER_RAUL, 0, format("Step into the crucible with me! Let us test our might!"), format("Outstanding!"))
	msgbox(format("I've come to the most treacherous environment in the region. I hope it will bring my training to new heights."), MSGBOX_NPC)
    end
}

script IgneousCaldera_1F_EventScript_ChamberDoor {
    lock
    if (flag(FLAG_IGNEOUS_CALDERA_CHAMBER_OPEN)) {
        msgbox("The way is open.", MSGBOX_AUTOCLOSE)
        release
        end
    }
    braillemsgbox(IgneousCaldera_1F_Braille_ChamberDoor1)
    delay(5)
    braillemsgbox(IgneousCaldera_1F_Braille_ChamberDoor2)
    checkitem(ITEM_REGIUS_CREST)
    if (var(VAR_RESULT)) {
        msgbox(format("Use the REGIUS CREST?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT)) {
            fadeoutbgm(1)
            setvar(VAR_0x8004, 0)  //vertical pan
            setvar(VAR_0x8005, 1)  //horizontal pan
            setvar(VAR_0x8006, 8)  //num shakes
            setvar(VAR_0x8007, 5)  //shake delay
            special(ShakeCamera)
            waitstate
            delay(30)
            setmetatile(18, 35, METATILE_Cave_SealedChamberEntrance_TopLeft, TRUE)
            setmetatile(19, 35, METATILE_Cave_SealedChamberEntrance_TopMid, TRUE)
            setmetatile(20, 35, METATILE_Cave_SealedChamberEntrance_TopRight, TRUE)
            setmetatile(18, 36, METATILE_Cave_SealedChamberEntrance_BottomLeft, TRUE)
            setmetatile(19, 36, METATILE_Cave_SealedChamberEntrance_BottomMid, FALSE)
            setmetatile(20, 36, METATILE_Cave_SealedChamberEntrance_BottomRight, TRUE)
            special(DrawWholeMapView)
            playse(SE_UNLOCK)
            setflag(FLAG_IGNEOUS_CALDERA_CHAMBER_OPEN)
            delay(30)
            fadeinbgm(0)
        }
    }
    release
    end
}

script IgneousCaldera_1F_EventScript_Registeel {
    lock
    waitse
    playmoncry(SPECIES_REGISTEEL, CRY_MODE_ENCOUNTER)
    delay(40)
    waitmoncry
    setwildbattle(SPECIES_REGISTEEL, 35)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	special(StartRegiBattle)
	waitstate
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
        setflag(FLAG_CAUGHT_REGISTEEL)
        goto(Common_EventScript_RemoveStaticPokemon)
    }
    else {
        msgbox(format("REGISTEEL is recharging…"), MSGBOX_AUTOCLOSE)
    }
    release
    end
}

raw `
IgneousCaldera_1F_Braille_ChamberDoor1:
	brailleformat 3, 0, 25, 19, 6, 3
	.braille "THE TITAN OF\n"
	.braille "STEEL IS\n"
	.braille "TEMPERED BY\n"
	.braille "ETERNAL FIRE$"

IgneousCaldera_1F_Braille_ChamberDoor2:
	brailleformat 3, 0, 25, 19, 6, 3
	.braille "THE MARK OF\n"
	.braille "THE COLOSSUS\n"
	.braille "WILL ROUSE\n"
	.braille "IT TO ACTION$"
`