mapscripts AridIsland_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_ARID_ISLAND)
		if (flag(FLAG_DEFEATED_ARID_ISLAND_FLY_GUY) && flag(FLAG_VISITED_KAPOK_ISLAND)) {
			setflag(FLAG_HIDE_ARID_ISLAND_FLY_GUY)
			removeobject(8)
		}
	}
}

script AridIsland_EventScript_FlyGuy {
	lock
	faceplayer
	if (flag(FLAG_DEFEATED_ARID_ISLAND_FLY_GUY)) {
		msgbox(format("Hey, it's you again!\pMy birds can take you to any island you've visited before.\pWhat do you say, want a lift?"), MSGBOX_YESNO)
		if (var(VAR_RESULT)) {
			setvar(VAR_0x800A, LAST_TALKED_TO_FLYING_TAXI)
			special(FieldLoadFlyMap)
		}
		else {
			msgbox(AridIsland_Text_FlyGuyComeBack, MSGBOX_DEFAULT)
		}
		release
		end
	}
	msgbox(format("Howdy!\pI've been using my bird POKéMON to FLY around the islands.\pMy birds need to stretch their wings before setting out again.\pWould you help me warm them up with a battle?"), MSGBOX_YESNO)
	if (!var(VAR_RESULT)) {
		msgbox(AridIsland_Text_FlyGuyIllBeHere, MSGBOX_DEFAULT)
		release
		end
	}
	trainerbattle(TRAINER_BATTLE_CONTINUE_SCRIPT_DETECT_NO_MUSIC, TRAINER_LESTER_1, 0, format("Alright!"), format("Amazing!"), AridIsland_EventScript_FlyGuy_PostBattle)
	release
	end
}

script AridIsland_EventScript_FlyGuy_PostBattle {
	setflag(FLAG_DEFEATED_ARID_ISLAND_FLY_GUY)
	msgbox(format("Hey, you have real talent as a TRAINER!\pI gotta thank you somehow…\nOh, I know!\pMy birds can take you to any island you've visited before.\pWhat do you say, want a lift?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
		setvar(VAR_0x800A, LAST_TALKED_TO_FLYING_TAXI)
		special(FieldLoadFlyMap)
	}
	else {
		msgbox(AridIsland_Text_FlyGuyComeBack, MSGBOX_DEFAULT)
	}
	release
	end
}

script AridIsland_EventScript_ItemProtein {
	finditem(ITEM_PROTEIN)
	end
}

script AridIsland_EventScript_AridIslandSign {
	msgbox(format("ARID ISLAND\nThe hot, dry market town."), MSGBOX_SIGN)
}

script AridIsland_EventScript_DesertSign {
	msgbox(format("Passage to ARID DESERT.\pWARNING: Severe sandstorms ahead."), MSGBOX_SIGN)
}

script AridIsland_EventScript_Boy1 {
	msgbox(format("All the MARTS around here\nare outdoors.\pThe cool ocean breeze makes the weather on this side of the island just perfect."), MSGBOX_NPC)
}

script AridIsland_EventScript_Boy2 {
	msgbox(format("I'm not allowed to play in\nthe desert!\pIt's easy to get lost in the\nsandstorm, and it's full of\lscary POKéMON!"), MSGBOX_NPC)
}

script AridIsland_EventScript_RuinManiac {
	msgbox(format("The ARID DESERT hides a great secret!\nIt's home to ancient ruins just\lbegging to be explored!\pAdventurers come from all over\njust to see them."), MSGBOX_NPC)
}

script AridIsland_EventScript_DrinkMart {
	lock
	message("The desert is hot!\nBeat the heat with a drink!")
	waitmessage
	pokemart(DrinkMartItems)
	msgbox("Come again soon!")
	release
}

mart DrinkMartItems {
	ITEM_BERRY_JUICE
	ITEM_FRESH_WATER
	ITEM_SODA_POP
	ITEM_LEMONADE
	ITEM_MOOMOO_MILK
}

script AridIsland_EventScript_GeneralMart {
	lock
	message("General goods here!\nNeed to stock up?")
	waitmessage
	pokemart(GeneralMartItems)
	msgbox("Thank you.")
	release
}

mart GeneralMartItems {
	ITEM_POTION
	ITEM_SUPER_POTION
	ITEM_POKE_BALL
	ITEM_GREAT_BALL
	ITEM_ANTIDOTE
	ITEM_PARALYZE_HEAL
	ITEM_BURN_HEAL
	ITEM_AWAKENING
	ITEM_REVIVE
	ITEM_ESCAPE_ROPE
	ITEM_REPEL
}

script AridIsland_EventScript_BerryMart {
	lock
	message("Berries for sale!\nGet them while they're ripe!")
	waitmessage
	pokemart(BerryMartItems)
	msgbox("Come again!")
	release
}

mart BerryMartItems {
	ITEM_ORAN_BERRY
	ITEM_LEPPA_BERRY
	ITEM_CHERI_BERRY
	ITEM_PECHA_BERRY
	ITEM_RAWST_BERRY
	ITEM_CHESTO_BERRY
	ITEM_ASPEAR_BERRY
	ITEM_PERSIM_BERRY
}

text AridIsland_Text_FlyGuyComeBack {
	format("I'll hang around here a little longer, so come back if you need a lift.")
}

text AridIsland_Text_FlyGuyIllBeHere {
	format("Aw, alright.\pI'll be right here if you change your mind.")
}