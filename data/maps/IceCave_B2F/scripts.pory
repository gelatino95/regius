mapscripts IceCave_B2F_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        if (!flag(FLAG_ICE_CAVE_CHAMBER_OPEN)) { //if chamber hasn't been opened yet, close the door
            setmetatile(16, 19, 0x3B7, TRUE)
            setmetatile(17, 19, 0x3B7, TRUE)
            setmetatile(18, 19, 0x3B7, TRUE)
            setmetatile(16, 20, METATILE_Cave_IceChamberBraille_Mid, TRUE)
            setmetatile(17, 20, METATILE_Cave_IceChamberBraille_Mid, TRUE)
            setmetatile(18, 20, METATILE_Cave_IceChamberBraille_Mid, TRUE)
        }
    }
}

script IceCave_B2F_EventScript_ItemAncientSlab {
    finditem(ITEM_ANCIENT_SLAB)
    end
}

script IceCave_B2F_EventScript_ItemTM07 {
    finditem(ITEM_TM07)
    end
}

script IceCave_B2F_EventScript_ChamberDoor {
    lock
    if (flag(FLAG_ICE_CAVE_CHAMBER_OPEN)) {
        msgbox("The way is open.", MSGBOX_AUTOCLOSE)
        release
        end
    }
    braillemsgbox(IceCave_B2F_Braille_ChamberDoor1)
    delay(5)
    braillemsgbox(IceCave_B2F_Braille_ChamberDoor2)
    checkitem(ITEM_REGIUS_CREST)
    if (var(VAR_RESULT)) {
        msgbox(format("Use the REGIUS CREST?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT)) {
            fadeoutbgm(1)
            setvar(VAR_0x8004, 0)  //vertical pan
            setvar(VAR_0x8005, 1)  //horizontal pan
            setvar(VAR_0x8006, 8)  //num shakes
            setvar(VAR_0x8007, 5)  //shake delay
            special(ShakeCamera)
            waitstate
            delay(30)
            setmetatile(16, 19, METATILE_Cave_IceChamberEntrance_TopLeft, TRUE)
            setmetatile(17, 19, METATILE_Cave_IceChamberEntrance_TopMid, TRUE)
            setmetatile(18, 19, METATILE_Cave_IceChamberEntrance_TopRight, TRUE)
            setmetatile(16, 20, METATILE_Cave_IceChamberEntrance_BottomLeft, TRUE)
            setmetatile(17, 20, METATILE_Cave_IceChamberEntrance_BottomMid, FALSE)
            setmetatile(18, 20, METATILE_Cave_IceChamberEntrance_BottomRight, TRUE)
            special(DrawWholeMapView)
            playse(SE_UNLOCK)
            setflag(FLAG_ICE_CAVE_CHAMBER_OPEN)
            delay(30)
            fadeinbgm(0)
        }
    }
    release
    end
}

script IceCave_B2F_EventScript_Regice {
    lock
    waitse
    playmoncry(SPECIES_REGICE, CRY_MODE_ENCOUNTER)
    delay(40)
    waitmoncry
    setwildbattle(SPECIES_REGICE, 30)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	special(StartRegiBattle)
	waitstate
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
        setflag(FLAG_CAUGHT_REGICE)
        goto(Common_EventScript_RemoveStaticPokemon)
    }
    else {
        msgbox(format("REGICE is rechargingâ€¦"), MSGBOX_AUTOCLOSE)
    }
    release
    end
}

raw `
IceCave_B2F_Braille_ChamberDoor1:
	brailleformat 3, 0, 25, 19, 6, 3
	.braille "THE TITAN OF\n"
	.braille "ICE SLEEPS\n"
	.braille "HERE FROZEN\n"
	.braille "IN TIME.$"

IceCave_B2F_Braille_ChamberDoor2:
	brailleformat 3, 0, 25, 19, 6, 3
	.braille "PRESENT THE\n"
	.braille "MARK OF THE\n"
	.braille "COLOSSUS\n"
	.braille "TO WAKE IT.$"
`